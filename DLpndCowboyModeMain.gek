scn DLpndCowboyModeMain

begin Function{}
    if GetGameLoaded

        ; Using PlaceAtMe creates persistent refs, so its better to make a non save baked TempCloneForm
        ref rClonedTumbleweed = TempCloneForm TumbleweedNV

        ; Giving it an EditorID so we can look it up in the Event Handler
        SetEditorID rClonedTumbleweed "ClonedTumbleweedNV"

        ; We know what the config file looks like, so grabbing keys directly
        array_var config = GetINISection "Character" "DLpnd\CowboySettings.ini" 1

        Player.AuxVarSetFLT "*MustHaveCowboyPerk" (config["MustHaveCowboyPerk"])
        Player.AuxVarSetFLT "*MustHaveCowboyWeapon" (config["MustHaveCowboyWeapon"])
        Player.AuxVarSetFLT "*MustHaveWildWasteland" (config["MustHaveWildWasteland"])
        Player.AuxVarSetFLT "*JumpDistance" (config["JumpDistance"])
        Player.AuxVarSetFLT "*TumbleweedPushForce" (config["TumbleweedPushForce"])

        SetEventHandler "OnStartCombat" DLpndCowboyModeEvent
        SetEventHandler "OnCombatEnd" DLpndCowboyModeClean
    endif
end
